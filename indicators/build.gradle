plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildToolsVersion '34.0.0'

    namespace 'net.alexandroid.utils.indicators'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            create('release', MavenPublication) {
                groupId = 'com.github.mtrezza'
                artifactId = 'indicators'
                version = '1.8.2'

                // Add the AAR file produced by the Android build
                afterEvaluate {
                    artifact("$buildDir/outputs/aar/${project.name}-release.aar")
                }

                // Add the sources JAR
                artifact(sourcesJar)
            }
        }

        repositories {
            maven {
                // Use a dummy repository for local publication
                url = uri("file:///Users/tmp/")
            }
        }
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
}
